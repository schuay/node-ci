# Copyright 2019 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//node/node_files.gni")
import("//node/node.gni")

config("external_config") {
  include_dirs = [ "openssl/include" ]
}

config("internal_config") {
  cflags_c = [
    "-Wno-implicit-function-declaration",
    "-Wno-constant-conversion",
    "-Wno-incompatible-pointer-types",
    "-Wno-constant-conversion",
  ]
  if (is_posix || is_fuchsia) {
    asmflags = [ "-fPIC" ]
    cflags = [ "-fPIC" ]
    ldflags = [ "-fPIC" ]
  }
  cflags_cc = [ "-Wno-sign-compare" ]
  include_dirs = [
    "openssl",
    "openssl/crypto/",
    "openssl/crypto/include/",
    "openssl/crypto/include/internal",
    "openssl/crypto/modes/",
    "openssl/crypto/ec/curve448",
    "openssl/crypto/ec/curve448/arch_32",
    "config/archs/linux-x86_64/no-asm/crypto",
    "config",
  ]
  if (is_win) {
    libs = [ "crypt32.lib" ]
  }
}

openssl_defines = [
  "NDEBUG",
  "OPENSSL_USE_NODELETE",
  "L_ENDIAN",
  "OPENSSL_PIC",
  "OPENSSL_NO_ASM",
  "OPENSSL_NO_HW",
  "OPENSSLDIR=\"/etc/ssl\"",
  "ENGINESDIR=\"/dev/null\"",
]

node_dep("openssl") {
  sources = node_files.openssl_sources
  defines = openssl_defines

  include_dirs = [
    "openssl",
    "openssl/crypto/",
    "openssl/crypto/include/",
    "openssl/crypto/modes/",
    "openssl/crypto/ec/curve448",
    "openssl/crypto/ec/curve448/arch_32",
    "openssl/config/archs/linux-x86_64/no-asm/crypto",
  ]

  public_configs = [ ":external_config" ]
  configs = [ ":internal_config" ]
}
